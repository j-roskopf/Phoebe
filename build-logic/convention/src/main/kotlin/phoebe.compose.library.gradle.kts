import phoebe.configurePhoebeKmp
import phoebe.libraryNamespace
import phoebe.libs
import org.jetbrains.kotlin.gradle.ExperimentalWasmDsl
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id("com.android.kotlin.multiplatform.library")
    kotlin("multiplatform")
    id("org.jetbrains.kotlin.plugin.compose")
    id("org.jetbrains.compose")
}

kotlin {
    configurePhoebeKmp(this)

    // Compose 1.12+ requires an executable wasmJs binary so Skiko can load for browser UI tests
    // (CMP-4906 / checkComposeUiTestConfigurationForWasmJs).
    @OptIn(ExperimentalWasmDsl::class)
    wasmJs {
        binaries.executable()
    }

    android {
        namespace = libraryNamespace()
        compileSdk = 37
        minSdk = 26
        androidResources {
            enable = true
        }
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_17)
        }
        withHostTest {
            isIncludeAndroidResources = true
            isReturnDefaultValues = true
        }
    }

    sourceSets {
        named("commonMain") {
            dependencies {
                implementation(libs.findLibrary("compose-runtime").get())
                implementation(libs.findLibrary("compose-foundation").get())
                implementation(libs.findLibrary("compose-material3").get())
                implementation(libs.findLibrary("compose-components-resources").get())
                implementation(libs.findLibrary("compose-ui-tooling-preview").get())
            }
        }
        findByName("androidHostTest")?.apply {
            kotlin.srcDir("src/androidUnitTest/kotlin")
        }
    }
}
